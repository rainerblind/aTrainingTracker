plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-parcelize'
}

def getGitHash = providers.exec {
    commandLine('git', 'rev-parse', '--short', 'HEAD')
}.standardOutput.asText.get().strip()

android {
    // Travis update separately
    compileSdkVersion = 36
    buildToolsVersion = '36.0.0'
    namespace = "com.atrainingtracker"

    compileOptions {
        sourceCompatibility = JavaVersion.toVersion("17")
        targetCompatibility = JavaVersion.toVersion("17")
    }

    defaultConfig {
        minSdkVersion 26 // Android 8
        targetSdkVersion 35 // Android 15
        multiDexEnabled = true
        versionName = "4.9.4"
        versionCode = 228
    }

    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        release {
            // storeFile file('/home/rainer/Dropbox/workspace/keystore')
            // keyAlias "aTrainingTrackerReleaseKey"
        }
    }

    buildFeatures {
        buildConfig = true
        compose = true
        viewBinding = true
    }

    buildTypes {
        debug {
            // Separate the debug build
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-${getGitHash}"
            debuggable = true
        }
        release {
            applicationIdSuffix ""

            minifyEnabled = false
            proguardFiles = [getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.txt']
            signingConfig = signingConfigs.release
            debuggable = false
        }
    }
    packagingOptions {
        resources {
            pickFirsts += ['META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE.txt', 'META-INF/LICENSE']
        }
    }

    lint {
        baseline = file('lint-baseline.xml')
        checkReleaseBuilds = true
        // TODO fix Range, some hundred occurrences
        disable = ['DuplicatePlatformClasses', 'MissingTranslation', 'Range']
        showAll = true
        textReport = true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

configurations {
    compile.exclude group: "junit", module: "junit"
}

dependencies {
    implementation 'org.apache.httpcomponents:fluent-hc:4.5.14'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpclient-cache:4.5.14'
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'
    implementation 'org.apache.httpcomponents:httpmime:4.5.14'

    implementation 'com.getpebble:pebblekit:4.0.1'
    implementation files('../ANT-Android-SDKs/ANT+_Android_SDK/API/antpluginlib_3-9-0.aar')

    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.work:work-runtime:2.11.0'
    implementation 'androidx.activity:activity:1.12.2'
    implementation 'androidx.activity:activity-compose:1.12.2'

    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.google.android.gms:play-services-maps:19.2.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    implementation platform('com.google.firebase:firebase-bom:34.7.0')
    implementation 'com.google.firebase:firebase-analytics' // No version needed with BOM

    implementation 'com.dropbox.core:dropbox-core-sdk:7.0.0'
    implementation 'com.dropbox.core:dropbox-android-sdk:7.0.0'
    implementation 'com.squareup.picasso:picasso:2.71828' // Consider using Glide or Coil
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.datastore:datastore:1.2.0'
    implementation 'androidx.datastore:datastore-preferences:1.2.0'
    implementation 'androidx.compose.foundation:foundation:1.10.0'
    implementation 'androidx.compose.material:material-icons-extended:1.7.8'
    implementation 'androidx.compose.material3:material3:1.4.0'
    implementation 'androidx.datastore:datastore-preferences-core:1.2.0'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.10.0'
    implementation 'androidx.compose.ui:ui-tooling:1.10.0'
    implementation 'androidx.compose.remote:remote-creation-core:1.0.0-alpha01'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.10.0'
    implementation 'androidx.compose.foundation:foundation-layout:1.10.3'
}

secrets {
    propertiesFileName = "secrets.properties"
}

def props = new Properties()
if (rootProject.file("release.properties").exists()) {
    props.load(new FileInputStream(rootProject.file("release.properties")))

    android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
} else {
    project.logger.info('INFO: Set the values storeFile, storePassword, keyAlias, and keyPassword in release.properties to sign the release.')
    android.buildTypes.release.signingConfig = null
}
